set(executable_name kubint)
set(src_path src)

if (UNIX)
    #name mangling, these functions are generated by the FortranCInterface cmake package and might be system dependent
    include(FortranCInterface)
    FortranCInterface_HEADER("${CMAKE_CURRENT_SOURCE_DIR}/include/FortranCInterface.h" MACRO_NAMESPACE "FC_")
    #produce a config.h to be included
    configure_file(${CMAKE_CURRENT_SOURCE_DIR}/config.h.cmake.in ${CMAKE_CURRENT_SOURCE_DIR}/include/config.h)  
    set(configFile "${CMAKE_CURRENT_SOURCE_DIR}/include/config.h") 
    
    include_directories(${CMAKE_CURRENT_SOURCE_DIR}/include) 
    add_definitions(-DHAVE_CONFIG_H=1)
endif(UNIX)

# Gather source files
file(GLOB source_files ${src_path}/*.cpp)

file(GLOB version_files ${kubint_version_number_include_path}/*.rc) 

# Define executable
add_executable(${executable_name} ${source_files}
                                  ${version_files})

# Set dependencies
set(oss_dependencies    deltares_common
                        deltares_common_c
                        deltares_common_mpi
                        kubint_f
                        nefis)
oss_include_libraries(${executable_name} oss_dependencies)

if (WIN32)
    # Set linker options
    message(STATUS "Setting target_link_options in windows")
    target_link_options(${executable_name} PRIVATE ${nologo_flag})

    if(CMAKE_Fortran_COMPILER_ID MATCHES "Intel")
        target_link_options(${executable_name} PRIVATE "/LIBPATH: $(IFORT_COMPILER21)/compiler/lib/intel64;")
    endif()
endif(WIN32)
# Set preprocessor definitions
if(WIN32)
    target_compile_definitions(${executable_name} PRIVATE "WIN32")
endif(WIN32)
if(UNIX)
    target_compile_definitions(${executable_name} PRIVATE "HAVE_CONFIG_H")
endif(UNIX)

# Explicitly link the libraries to the target or the compilation will fail as the linker cannot find the Fortran libs
target_link_libraries(${executable_name} ${oss_dependencies})

# Define how the files should be structured within Visual Studio
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES ${source_files})
source_group("Resource" FILES ${version_files})
set_target_properties (${executable_name} PROPERTIES FOLDER tools_gpl/kubint)

# Set post-build step
set(install_dir ${CMAKE_BINARY_DIR})
set(build_dir ${CMAKE_BINARY_DIR})

post_build_target (${executable_name}
                   ${install_dir} 
                   ${build_dir} 
                   ${checkout_src_root} 
                   ${executable_name})

install(TARGETS ${executable_name} RUNTIME  DESTINATION bin)
