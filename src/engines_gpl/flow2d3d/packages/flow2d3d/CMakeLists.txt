set(library_name flow2d3d)

# Set directory which contains all source files
set(src_path src)
set(include_path include)

set(dd_path ${src_path}/dd)
set(dd_iterators_path ${src_path}/dd/iterators)
set(dd_mapper_path ${src_path}/dd/mapper)

# Define include directories
get_module_include_path(${checkout_src_root}/${d_hydro_lib_module} 
                        ${library_name}
                        d_hydro_lib_include_path)

get_module_include_path(${checkout_src_root}/${deltares_common_c_module} 
                        ${library_name}
                        deltares_common_c_include_path)

get_module_include_path(${checkout_src_root}/${esmfsm_module} 
                        ${library_name} 
                        esmfsm_module_include_path)

message(STATUS "Configuring '${library_name}' with the following deltares_common_c include path: '${deltares_common_c_include_path}'")
message(STATUS "Configuring '${library_name}' with the following d_hydro_lib include path: '${d_hydro_lib_include_path}'")
message(STATUS "Configuring '${library_name}' with the following esmfsm include path: '${esmfsm_module_include_path}'")

include_directories(${include_path}
                    ${deltares_common_c_include_path}
                    ${d_hydro_lib_include_path}
                    ${esmfsm_module_include_path}
                    ${dd_path}
                    ${dd_iterators_path}
                    ${dd_mapper_path}
                    ${pthreads_path}
                    ${mpi_include_path}
                    ${expat_include_path})

# Set version file variables
set(version_path ${CMAKE_CURRENT_SOURCE_DIR}/../) # Path to determine the version number from
set(flow2d3d_version_path ${CMAKE_CURRENT_SOURCE_DIR}/${include_path})

set(ini_version_file ${flow2d3d_version_path}/version_number.ini)
set(h_version_file ${flow2d3d_version_path}/flow2d3d_version.h)

# Generate version files
generate_version_files(${h_version_file} 
                       ${ini_version_file})

if (UNIX)
    #name mangling, these functions are generated by the FortranCInterface cmake package and might be system dependent
    include(FortranCInterface)
    FortranCInterface_HEADER("${CMAKE_CURRENT_SOURCE_DIR}/include/FortranCInterface.h" MACRO_NAMESPACE "FC_")
    #produce a config.h to be included
    configure_file(${CMAKE_CURRENT_SOURCE_DIR}/config.h.cmake.in ${CMAKE_CURRENT_SOURCE_DIR}/include/config.h)  
    set(configFile "${CMAKE_CURRENT_SOURCE_DIR}/include/config.h") 
    
    include_directories(${NETCDF_INCLUDEDIR}) 
    add_definitions(-DHAVE_CONFIG_H)
endif(UNIX)

# Gather source files 
file(GLOB header_files ${include_path}/*.h)
file(GLOB header_dd_files ${dd_path}/*.h)
file(GLOB header_dd_iterators_files ${dd_iterators_path}/*.h)
file(GLOB header_dd_mapper_files ${dd_mapper_path}/*.h)

file(GLOB source_root_files ${src_path}/*.cpp)
file(GLOB source_dd_files ${dd_path}/*.cpp)
file(GLOB source_dd_iterators_files ${dd_iterators_path}/*.cpp)
file(GLOB source_dd_mapper_files ${dd_mapper_path}/*.cpp)

# Define library
add_library(${library_name} SHARED  ${header_files}
                                    ${header_dd_files}
                                    ${header_dd_iterators_files}
                                    ${header_dd_mapper_files}
                                    ${source_root_files}
                                    ${source_dd_files}
                                    ${source_dd_iterators_files}
                                    ${source_dd_mapper_files})

# Define prebuild events
prebuild_version_number(${library_name} ${h_version_file} 
                                        ${version_path}
                                        ${ini_version_file})

# Set dependencies
set(oss_dependencies_general    delftio
                        delftio_shm
                        deltares_common
                        deltares_common_mpi
                        deltares_common_c
                        d_hydro_lib
                        gridgeom
                        ec_module
                        esmfsm
                        esmfsm_c
                        esmfsm_version_number
                        flow2d3d_data
                        flow2d3d_io
                        flow2d3d_io_dol_f
                        flow2d3d_kernel
                        flow2d3d_kernel_dd_f
                        flow2d3d_manager
                        flow2d3d_plugin_culvert_c
                        flow2d3d_plugin_user
                        io_hyd
                        io_netcdf
                        kdtree_wrapper
                        kdtree2
                        morphology_data
                        morphology_io
                        morphology_kernel
                        morphology_plugins_c
                        nefis
                        triangle_c) 
# Add dependencies
if (UNIX)
    # the `pkg_check_modules` function is created with this call
    find_package(PkgConfig REQUIRED)

    # these calls create special `PkgConfig::<MODULE>` variables
    pkg_check_modules(NETCDF REQUIRED IMPORTED_TARGET netcdf)
    pkg_check_modules(NETCDF_FTN REQUIRED IMPORTED_TARGET netcdf-fortran)

    set(oss_dependencies ${oss_dependencies_general})

    target_link_libraries(${library_name}
         ${oss_dependencies}
         PkgConfig::NETCDF
         PkgConfig::NETCDF_FTN)
    
endif(UNIX)
if (WIN32)
    set(oss_dependencies ${oss_dependencies_general}
                        netcdf4
                        netcdff)

    target_link_libraries(${library_name} ${oss_dependencies})
endif(WIN32)
oss_include_libraries(${library_name} oss_dependencies)

if (WIN32)
    # Set linker properties
    message(STATUS "Setting linker properties in windows")
    target_link_directories(${library_name}
                            PRIVATE
                            "${checkout_src_root}/third_party_open/netcdf/${netcdf_version}/lib"
                            "${mpi_library_path}"
                            "${expat_library_path_win}"
                            "${checkout_src_root}/third_party_open/pthreads/bin/x64")

    target_link_libraries(${library_name}
                            "libexpat.lib"
                            "${mpi_fortran_library}"
                            "pthreadVC2.lib"
                            "wsock32.lib"
                            "netapi32.lib"
                            "comctl32.lib"
                            "user32.lib"
                            "advapi32.lib"
                            "comdlg32.lib"
                            "gdi32.lib"
                            "winspool.lib" 
                            "netcdf.lib")

    # Set linker options
    message(STATUS "Setting target_link_options in windows")
    target_link_options(${library_name} PRIVATE ${nologo_flag})
endif(WIN32)

# Define how the files should be structured within Visual Studio
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES ${header_files}
                                                    ${header_dd_files}
                                                    ${header_dd_iterators_files}
                                                    ${header_dd_mapper_files}
                                                    ${source_root_files}
                                                    ${source_dd_files}
                                                    ${source_dd_iterators_files}
                                                    ${source_dd_mapper_files})
set_target_properties (${library_name} PROPERTIES FOLDER engines_gpl/flow2d3d)

# Set post-build step
set(install_dir ${CMAKE_BINARY_DIR})
set(build_dir ${CMAKE_BINARY_DIR})

post_build_target (${library_name}
                   ${install_dir} 
                   ${build_dir} 
                   ${checkout_src_root} 
                   ${library_name})
